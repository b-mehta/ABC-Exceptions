/-
Copyright (c) 2025 Bhavik Mehta, Arend Mellendijk. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Arend Mellendijk
-/

import Mathlib.Analysis.SpecialFunctions.Log.Base
import Mathlib.Analysis.SpecialFunctions.Pow.Asymptotics
import Mathlib.Data.Nat.Squarefree
import Mathlib.Data.Real.StarOrdered
import Mathlib.Order.CompletePartialOrder

import ABCExceptions.ForMathlib.RingTheory.Radical

open Finset UniqueFactorizationMonoid

/--
The set (as a `Finset`) of exceptions to the abc conjecture at `Œµ` inside [1, X] ^ 3, in particular
the set of triples `(a, b, c)` which are
* pairwise coprime,
* contained in `[1, X] ^ 3`,
* satisfy `a + b = c`,
* have `radical (a * b * c) < c ^ (1 - Œµ)`

Note this has a slight difference from the usual formulation, which has
`radical (a * b * c) ^ (1 + Œµ) < c` instead.
-/
noncomputable def Finset.abcExceptionsBelow (Œµ : ‚Ñù) (X : ‚Ñï) : Finset (‚Ñï √ó ‚Ñï √ó ‚Ñï) :=
  (Finset.Icc (1, 1, 1) (X, X, X)).filter fun ‚ü®a, b, c‚ü© ‚Ü¶
    a.Coprime b ‚àß a.Coprime c ‚àß b.Coprime c ‚àß
    a + b = c ‚àß
    radical (a * b * c) < (c ^ (1 - Œµ) : ‚Ñù)

@[simp]
theorem Finset.mem_abcExceptionsBelow (Œµ : ‚Ñù) (X : ‚Ñï) (a b c : ‚Ñï) :
    ‚ü®a, b, c‚ü© ‚àà abcExceptionsBelow Œµ X ‚Üî
      a.Coprime b ‚àß a.Coprime c ‚àß b.Coprime c ‚àß
      a + b = c ‚àß
      radical (a * b * c) < (c ^ (1 - Œµ) : ‚Ñù) ‚àß
      (a, b, c) ‚àà Set.Icc (1, 1, 1) (X, X, X) := by
  simp [abcExceptionsBelow]
  tauto

@[gcongr]
lemma Finset.abcExceptionsBelow_mono_right {Œµ : ‚Ñù} {X Y : ‚Ñï} (hXY : X ‚â§ Y) :
    abcExceptionsBelow Œµ X ‚äÜ abcExceptionsBelow Œµ Y := by
  rintro ‚ü®a, b, c‚ü©
  simp +contextual
  omega

@[gcongr]
lemma Finset.abcExceptionsBelow_mono_left {Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù} {X : ‚Ñï} (hŒµ : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) :
    abcExceptionsBelow Œµ‚ÇÇ X ‚äÜ abcExceptionsBelow Œµ‚ÇÅ X := by
  rintro ‚ü®a, b, c‚ü©
  simp +contextual only [mem_abcExceptionsBelow, Nat.Coprime, Set.mem_Icc, Prod.mk_le_mk, and_self,
    and_true, true_and, and_imp]
  rintro - - - - h - - hc - - -
  refine h.trans_le ?_
  gcongr
  simpa

@[gcongr]
lemma Finset.abcExceptionsBelow_mono {Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù} {X Y : ‚Ñï}
    (hŒµ : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) (hXY : X ‚â§ Y) :
    abcExceptionsBelow Œµ‚ÇÇ X ‚äÜ abcExceptionsBelow Œµ‚ÇÅ Y :=
  (abcExceptionsBelow_mono_right hXY).trans (abcExceptionsBelow_mono_left hŒµ)

/--
The number of exceptions to the abc conjecture for a given `Œµ` which are bounded by `X`.
-/
noncomputable def countTriples (Œµ : ‚Ñù) (X : ‚Ñï) : ‚Ñï := #(abcExceptionsBelow Œµ X)

/--
The set of exceptions to the abc conjecture for `Œµ`, in particular
the set of triples `(a, b, c)` which are
* pairwise coprime,
* positive,
* satisfy `a + b = c`,
* have `radical (a * b * c) ^ (1 + Œµ) < c`
-/
def abcExceptions (Œµ : ‚Ñù) : Set (‚Ñï √ó ‚Ñï √ó ‚Ñï) :=
  { (a, b, c) : ‚Ñï √ó ‚Ñï √ó ‚Ñï |
    0 < a ‚àß 0 < b ‚àß 0 < c ‚àß
    a.Coprime b ‚àß a.Coprime c ‚àß b.Coprime c ‚àß
    a + b = c ‚àß
    radical (a * b * c) ^ (1 + Œµ) < (c : ‚Ñù) }

@[simp]
theorem mem_abcExceptions (Œµ : ‚Ñù) (a b c : ‚Ñï) :
    ‚ü®a, b, c‚ü© ‚àà abcExceptions Œµ ‚Üî
      0 < a ‚àß 0 < b ‚àß 0 < c ‚àß
      a.Coprime b ‚àß a.Coprime c ‚àß b.Coprime c ‚àß
      a + b = c ‚àß
      radical (a * b * c) ^ (1 + Œµ) < (c : ‚Ñù) := Iff.rfl

@[gcongr]
lemma abcExceptions_mono {Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù} (hŒµ : Œµ‚ÇÇ ‚â§ Œµ‚ÇÅ) :
    abcExceptions Œµ‚ÇÅ ‚äÜ abcExceptions Œµ‚ÇÇ := by
  rintro ‚ü®a, b, c‚ü©
  simp +contextual only [mem_abcExceptions, Prod.mk_le_mk, and_imp, true_and, Nat.Coprime]
  rintro ha hb hc - - - - habc
  refine habc.trans_le' ?_
  gcongr
  simp [Nat.one_le_cast, Nat.add_one_le_iff, Nat.radical_pos]

lemma abcExceptions_subset_Ici_one (Œµ : ‚Ñù) : abcExceptions Œµ ‚äÜ Set.Ici 1 := by
  rintro ‚ü®a, b, c‚ü©
  simp only [mem_abcExceptions, Set.mem_Ici, and_imp, ‚Üê Prod.mk_one_one, Prod.mk_le_mk,
    Nat.add_one_le_iff]
  omega

/-- The abc conjecture: the set of exceptional triples is finite. -/
def abcConjecture : Prop := ‚àÄ Œµ > 0, (abcExceptions Œµ).Finite

open Topology in
lemma abcConjecture_iff_eventually :
    abcConjecture ‚Üî ‚àÄ·∂† Œµ in ùìù[>] 0, (abcExceptions Œµ).Finite := by
  constructor
  ¬∑ intro (h : ‚àÄ _, _)
    simp +contextual [eventually_nhdsWithin_iff, h]
  ¬∑ intro h Œµ hŒµ
    suffices ‚àÄ·∂† (Œ¥ : ‚Ñù) in ùìù[>] 0, (abcExceptions Œµ).Finite by simpa
    filter_upwards [h, eventually_nhdsWithin_of_eventually_nhds (eventually_lt_nhds hŒµ)]
      with Œ¥ hŒ¥ hŒ¥Œµ using hŒ¥.subset (abcExceptions_mono hŒ¥Œµ.le)

lemma abcExceptionsBelow_eq_abcExceptions_inter (Œµ : ‚Ñù) (X : ‚Ñï) (hŒµ : Œµ < 1) :
    abcExceptionsBelow Œµ X =
      abcExceptions ((1 - Œµ)‚Åª¬π - 1) ‚à© Set.Icc (1, 1, 1) (X, X, X) := by
  ext ‚ü®a, b, c‚ü©
  suffices radical (a * b * c) ^ (1 - Œµ)‚Åª¬π < (c : ‚Ñù) ‚Üî radical (a * b * c) < (c : ‚Ñù) ^ (1 - Œµ) by
    simp [Nat.add_one_le_iff]
    tauto
  rw [Real.rpow_inv_lt_iff_of_pos (by simp) (by simp) (by simpa)]

lemma abcExceptionsBelow_eq_abcExceptions_inter' (Œµ : ‚Ñù) (X : ‚Ñï) (hŒµ : 0 < Œµ) :
    abcExceptionsBelow (1 - (1 + Œµ)‚Åª¬π) X =
      abcExceptions Œµ ‚à© Set.Icc (1, 1, 1) (X, X, X) := by
  rw [abcExceptionsBelow_eq_abcExceptions_inter _ _]
  ¬∑ simp
  ¬∑ simp [add_pos zero_lt_one hŒµ]

open Asymptotics Filter

-- in a PR to mathlib
theorem ciSup_eq_monotonicSequenceLimit {Œ± : Type*} [ConditionallyCompleteLattice Œ±]
    [WellFoundedGT Œ±] (a : ‚Ñï ‚Üío Œ±) (ha : BddAbove (Set.range a)) :
    iSup a = monotonicSequenceLimit a := by
  refine (ciSup_le fun m => ?_).antisymm (le_ciSup ha _)
  rcases le_or_lt m (monotonicSequenceLimitIndex a) with hm | hm
  ¬∑ exact a.monotone hm
  ¬∑ obtain h := WellFoundedGT.monotone_chain_condition a
    exact (Nat.sInf_mem (s := {n | ‚àÄ m, n ‚â§ m ‚Üí a n = a m}) h m hm.le).ge

lemma forall_increasing' {Œ± : Type*} (f : ‚Ñï ‚Üí Set Œ±) (hf : Monotone f)
    (hf' : ‚àÄ n, (f n).Finite)
    {C : ‚Ñï} (hC : ‚àÄ n, (f n).ncard ‚â§ C) : (‚ãÉ n, f n).Finite := by
  by_contra!
  obtain ‚ü®t, ht, ht', ht''‚ü© := Set.Infinite.exists_subset_ncard_eq this (C + 1)
  lift t to Finset Œ± using ht'
  obtain ‚ü®i, hi‚ü© := hf.directed_le.exists_mem_subset_of_finset_subset_biUnion ht
  replace hC : (f i).ncard ‚â§ C := hC i
  have := Set.ncard_le_ncard hi (hf' _)
  omega

lemma forall_increasing {Œ± : Type*} (f : ‚Ñï ‚Üí Set Œ±) (hf : Monotone f)
    {s : Set Œ±} (hf' : ‚àÄ n, (s ‚à© f n).Finite)
    {C : ‚Ñï} (hC : ‚àÄ n, (s ‚à© f n).ncard ‚â§ C) : (s ‚à© ‚ãÉ n, f n).Finite := by
  rw [Set.inter_iUnion]
  refine forall_increasing' _ ?_ hf' hC
  intro a b hab
  exact Set.inter_subset_inter_right _ (hf hab)

lemma abcConjecture_iff :
    abcConjecture ‚Üî ‚àÄ Œµ > 0, Œµ < 1 ‚Üí (countTriples Œµ ¬∑ : ‚Ñï ‚Üí ‚Ñù) =O[atTop] (fun _ ‚Ü¶ (1 : ‚Ñù)) := by
  simp only [isBigO_one_nat_atTop_iff]
  constructor
  ¬∑ intro h Œµ hŒµ‚ÇÄ hŒµ‚ÇÅ
    have habc := h ((1 - Œµ)‚Åª¬π - 1) (by simp [sub_pos, one_lt_inv‚ÇÄ, *])
    use (abcExceptions ((1 - Œµ)‚Åª¬π - 1)).ncard
    intro n
    rw [Real.norm_natCast, Nat.cast_le, countTriples,
      ‚Üê Set.ncard_coe_Finset, abcExceptionsBelow_eq_abcExceptions_inter _ _ hŒµ‚ÇÅ]
    exact Set.ncard_le_ncard Set.inter_subset_left habc
  ¬∑ intro h Œµ hŒµ
    obtain ‚ü®C, hC‚ü© := h (1 - (1 + Œµ)‚Åª¬π)
      (by simp [inv_lt_one_of_one_lt‚ÇÄ, hŒµ]) (by simp [add_pos, hŒµ])
    have hC‚ÇÄ : 0 ‚â§ C := (hC 0).trans' (by simp)
    simp_rw [Real.norm_natCast, countTriples, ‚Üê Nat.le_floor_iff hC‚ÇÄ,
      ‚Üê Set.ncard_coe_Finset, abcExceptionsBelow_eq_abcExceptions_inter' Œµ _ hŒµ] at hC
    have : ‚ãÉ n, Set.Icc (1, 1, 1) (n, n, n) = Set.Ici 1 := by
      ext ‚ü®i, j, k‚ü©
      simp only [Set.mem_iUnion, Set.mem_Icc, Prod.mk_le_mk, exists_and_left, ‚Üê Prod.mk_one_one,
        Set.mem_Ici, and_iff_left_iff_imp, and_imp]
      rintro - - -
      exact ‚ü®max i (max j k), by simp‚ü©
    rw [‚Üê Set.inter_eq_left.2 (abcExceptions_subset_Ici_one Œµ), ‚Üê this]
    refine forall_increasing _ ?_ ?_ hC
    ¬∑ intro n m hnm
      dsimp
      gcongr
    ¬∑ intro n
      exact (Set.finite_Icc (1, 1, 1) (n, n, n)).inter_of_right _

/-- We define reals `x` and `X` to be similar if `x ‚àà [X, 2X]`. -/
def similar (x X : ‚Ñù) : Prop := x ‚àà Set.Icc X (2 * X)

local infixr:36 " ~ " => similar

theorem similar_pow_natLog (x : ‚Ñï) (hx : x ‚â† 0) : x ~ 2 ^ Nat.log 2 x := by
  simp only [similar, Set.mem_Icc]
  norm_cast
  constructor
  ¬∑ refine Nat.pow_log_le_self 2 hx
  ¬∑ rw [‚Üê Nat.pow_succ']
    exact (Nat.lt_pow_succ_log_self (by omega) _).le

open Classical in
/-- The finite set of exceptions `(a, b, c)` to the abc conjecture for which `X/2 ‚â§ c ‚â§ X` and
  `rad a ~ X^Œ±`, `rad b ~ X^Œ≤`, `rad c ~ X^Œ≥`. `S*` counts the size of this set. -/
noncomputable def dyadicPoints (Œ± Œ≤ Œ≥ : ‚Ñù) (X : ‚Ñï) : Finset (‚Ñï √ó ‚Ñï √ó ‚Ñï) :=
  (Finset.Icc (1, 1, 1) (2*X, 2*X, 2*X)).filter fun ‚ü®a, b, c‚ü© ‚Ü¶
    a.Coprime b ‚àß a.Coprime c ‚àß b.Coprime c ‚àß
    a + b = c ‚àß
    (radical a : ‚Ñï) ~ (X ^ Œ± : ‚Ñù) ‚àß
    (radical b : ‚Ñï) ~ (X ^ Œ≤ : ‚Ñù) ‚àß
    (radical c : ‚Ñï) ~ (X ^ Œ≥ : ‚Ñù) ‚àß
    X ‚â§ 2 * c ‚àß c ‚â§ X

@[simp]
theorem mem_dyadicPoints (Œ± Œ≤ Œ≥ : ‚Ñù) (X : ‚Ñï) (a b c : ‚Ñï) :
    ‚ü®a, b, c‚ü© ‚àà dyadicPoints Œ± Œ≤ Œ≥ X ‚Üî
      0 < a ‚àß 0 < b ‚àß 0 < c ‚àß
      a.Coprime b ‚àß a.Coprime c ‚àß b.Coprime c ‚àß
      a + b = c ‚àß
      (radical a : ‚Ñï) ~ (X ^ Œ± : ‚Ñù) ‚àß
      (radical b : ‚Ñï) ~ (X ^ Œ≤ : ‚Ñù) ‚àß
      (radical c : ‚Ñï) ~ (X ^ Œ≥ : ‚Ñù) ‚àß
      X ‚â§ 2 * c ‚àß c ‚â§ X := by
  simp only [dyadicPoints, Finset.mem_filter, Finset.mem_Icc, Prod.mk_le_mk, Nat.add_one_le_iff,
    and_assoc, and_congr_right_iff, similar, Set.mem_Icc, Nat.cast_le, ‚Üê and_assoc,
    and_congr_left_iff]
  intro ha hb hc hc_le_X hX_le_c hrc hrb hra habc hbc hac
  omega

/--
This is $$S^*_{Œ±,Œ≤,Œ≥}(X)$$ in the paper and blueprint.
-/
noncomputable def refinedCountTriplesStar (Œ± Œ≤ Œ≥ : ‚Ñù) (X : ‚Ñï) : ‚Ñï := #(dyadicPoints Œ± Œ≤ Œ≥ X)

/-- The set over which we take the supremum in lemma 2.2. -/
private noncomputable def indexSet (Œµ : ‚Ñù) (X : ‚Ñï) : Finset (‚Ñï √ó ‚Ñï √ó ‚Ñï √ó ‚Ñï) :=
  (Finset.Icc 0 (Nat.log 2 X)) √óÀ¢ (Finset.Icc 0 (Nat.log 2 X)) √óÀ¢
  (Finset.Icc 0 (Nat.log 2 X)) √óÀ¢ (Finset.Icc 1 (Nat.log 2 X+1)) |>.filter fun ‚ü®i, j, k, n‚ü© ‚Ü¶
    i + j + k ‚â§ (1 - Œµ) * n

private theorem card_indexSet_le (Œµ : ‚Ñù) (X : ‚Ñï) :
    (indexSet Œµ X).card ‚â§ (Nat.log 2 X + 1) ^ 4 := by
  apply (Finset.card_filter_le ..).trans
  simp only [card_product, Nat.card_Icc, tsub_zero, add_tsub_cancel_right]
  linear_combination

@[simp]
private theorem mem_indexSet (Œµ : ‚Ñù) (X : ‚Ñï) (i j k n : ‚Ñï) :
    ‚ü®i, j, k, n‚ü© ‚àà indexSet Œµ X ‚Üî
      i ‚â§ Nat.log 2 X ‚àß j ‚â§ Nat.log 2 X ‚àß k ‚â§ Nat.log 2 X ‚àß
      1 ‚â§ n ‚àß n ‚â§ Nat.log 2 X + 1 ‚àß i + j + k ‚â§ (1 - Œµ) * n := by
  simp [indexSet]
  norm_cast
  aesop

theorem Nat.Coprime.isRelPrime (a b : ‚Ñï) (h : a.Coprime b) : IsRelPrime a b := by
  rw [‚Üê Nat.coprime_iff_isRelPrime]
  exact h

theorem Finset.abcExceptionsBelow_subset_union_dyadicPoints (Œµ : ‚Ñù) (X : ‚Ñï) :
    Finset.abcExceptionsBelow Œµ X ‚äÜ
      (indexSet Œµ X).biUnion fun ‚ü®i, j, k, n‚ü© ‚Ü¶
        dyadicPoints (i / n : ‚Ñù) (j / n : ‚Ñù) (k / n : ‚Ñù) (2 ^ n) := by
  rintro ‚ü®a, b, c‚ü©
  simp only [mem_abcExceptionsBelow, Set.mem_Icc, Prod.mk_le_mk, Finset.mem_biUnion,
    mem_dyadicPoints, Nat.cast_pow, Nat.cast_ofNat, Prod.exists, mem_indexSet, and_imp]
  intro hab hac hbc habc hrad h1a h1b h1c haX hbX hcX
  have hŒµ : 0 ‚â§ 1 - Œµ := by
    by_contra!
    have h‚ÇÅ : (1 : ‚Ñù) ‚â§ radical (a * b * c) := mod_cast (Nat.radical_pos _)
    have h‚ÇÇ : (c : ‚Ñù) ^ (1 - Œµ) < 1 :=
      Real.rpow_lt_one_of_one_lt_of_neg (by norm_cast; omega) (by assumption)
    exact (hrad.trans h‚ÇÇ).not_le h‚ÇÅ
  have {a : ‚Ñï} (ha : 1 ‚â§ a) (haX : a ‚â§ X) : Nat.log 2 (radical a) ‚â§ Nat.log 2 X := by
    apply Nat.log_mono_right ((Nat.radical_le_self_iff.2 (by omega)).trans haX)
  let n := Nat.log 2 c + 1
  have hcn : c ‚â§ 2 ^ n := (Nat.lt_pow_succ_log_self one_lt_two c).le
  refine ‚ü®Nat.log 2 (radical a), Nat.log 2 (radical b), Nat.log 2 (radical c), n,
    ‚ü®this h1a haX, this h1b hbX, this h1c hcX, by omega, ?_, ?_‚ü©, by omega, by omega, by omega,
    hab, hac, hbc, habc, ?_‚ü©
  ¬∑ simp [n, Nat.log_mono_right hcX]
  ¬∑ -- Here we prove that Œ± + Œ≤ + Œ≥ ‚â§ 1 - Œµ
    have : radical (a * b * c) = radical a * radical b * radical c := by
      rw [radical_mul (a := a*b) (b := c), radical_mul]
      ¬∑ convert hab.isRelPrime
      exact hac.mul hbc |>.isRelPrime
    rw [this] at hrad
    clear this
    have := calc
      (2:‚Ñù) ^ (Nat.log 2 (radical a) + Nat.log 2 (radical b) + Nat.log 2 (radical c)) ‚â§
        (radical a : ‚Ñï) * (radical b : ‚Ñï) * (radical c : ‚Ñï) := by
        norm_cast
        simp_rw [Nat.pow_add]
        gcongr <;>
        ¬∑ apply Nat.pow_log_le_self
          exact radical_ne_zero
      _ ‚â§ ‚Üëc ^ (1 - Œµ) := by
        exact_mod_cast hrad.le
      _ ‚â§ (2:‚Ñù) ^ (n * (1 - Œµ)) := by
        norm_cast
        rw [Real.rpow_natCast_mul (by norm_num)]
        gcongr
        norm_cast
    rw [‚Üê Real.rpow_le_rpow_left_iff (show 1 < (2 : ‚Ñù) by norm_num)]
    norm_cast at this ‚ä¢
    convert this using 1
    ring_nf
  have {a : ‚Ñï} : (2 ^ n : ‚Ñù) ^ (Nat.log 2 (radical a) / n : ‚Ñù) =
      2 ^ Nat.log 2 (radical a) := by
    rw [‚Üê Real.rpow_natCast_mul (by norm_num)]
    have : n * (Nat.log 2 (radical a) / n : ‚Ñù) = Nat.log 2 (radical a) := by
      rw [mul_div_cancel‚ÇÄ]
      simp [n]
      norm_cast
    rw [this]
    simp
  have hc2 : 2 ‚â§ c := by omega
  simp_rw [this]
  have radical_similar {a : ‚Ñï} : (radical a : ‚Ñï) ~ 2 ^ (Nat.log 2 (radical a)) :=
    similar_pow_natLog (radical a) radical_ne_zero
  refine ‚ü®radical_similar, radical_similar, radical_similar, ?_, hcn‚ü©
  simp only [Nat.pow_succ', Nat.ofNat_pos, mul_le_mul_left, n]
  apply Nat.pow_log_le_self
  omega

theorem sum_le_card_mul_sup {Œπ : Type*} (f : Œπ ‚Üí ‚Ñï) (s : Finset Œπ) :
    ‚àë i ‚àà s, f i ‚â§ s.card * s.sup f := calc
  ‚àë i ‚àà s, f i ‚â§ ‚àë i ‚àà s, s.sup f := by
    apply Finset.sum_le_sum
    intro i hi
    exact Finset.le_sup hi
  _ = s.card * s.sup f := by
    simp

theorem card_union_dyadicPoints_le_log_pow_mul_sup (Œµ : ‚Ñù) (X : ‚Ñï) :
    ((indexSet Œµ X).biUnion fun ‚ü®i, j, k, n‚ü© ‚Ü¶
      dyadicPoints (i / n : ‚Ñù) (j / n : ‚Ñù) (k / n : ‚Ñù) (2^n)).card ‚â§
  (Nat.log 2 X+1)^4 * (indexSet Œµ X).sup fun ‚ü®i, j, k, n‚ü© ‚Ü¶
    refinedCountTriplesStar (i / n : ‚Ñù) (j / n : ‚Ñù) (k / n : ‚Ñù) (2^n) := by
  apply (Finset.card_biUnion_le ..).trans
  simp only
  apply (sum_le_card_mul_sup _ _).trans
  gcongr
  ¬∑ apply card_indexSet_le
  ¬∑ rfl

/-- The supremum that appears in lemma 2.2, taken over a finite subset of Œ±, Œ≤, Œ≥ > 0 such that
  Œ± + Œ≤ + Œ≥ ‚â§ 1 - Œµ -/
noncomputable def dyadicSupBound (Œµ : ‚Ñù) (X : ‚Ñï) : ‚Ñï :=
  (indexSet Œµ X).sup fun ‚ü®i, j, k, n‚ü© ‚Ü¶
    refinedCountTriplesStar (i / n : ‚Ñù) (j / n : ‚Ñù) (k / n : ‚Ñù) (2^n)

theorem countTriples_le_log_pow_mul_sup (Œµ : ‚Ñù) (X : ‚Ñï) :
    countTriples Œµ X ‚â§ (Nat.log 2 X + 1) ^ 4 * dyadicSupBound Œµ X := by
  simp_rw [countTriples, dyadicSupBound, refinedCountTriplesStar]
  apply le_trans _ (card_union_dyadicPoints_le_log_pow_mul_sup Œµ X)
  apply Finset.card_le_card
  exact Finset.abcExceptionsBelow_subset_union_dyadicPoints Œµ X

theorem Real.natLog_isBigO_logb (b : ‚Ñï) :
    (fun x : ‚Ñï ‚Ü¶ (Nat.log b x : ‚Ñù)) =O[atTop] (fun x : ‚Ñï ‚Ü¶ Real.logb b x) := by
  apply IsBigO.of_bound'
  filter_upwards with x
  rw [Real.norm_natCast, Real.norm_eq_abs]
  exact (Real.natLog_le_logb _ _).trans (le_abs_self _)

theorem Real.logb_isBigO_log (b : ‚Ñù) :
    logb b =O[atTop] log :=
  .of_bound |Real.log b|‚Åª¬π <| by filter_upwards using by simp [Real.logb, div_eq_inv_mul]

theorem Real.log_isBigO_logb (b : ‚Ñù) (hb : 1 < b) :
    log =O[atTop] logb b :=
  .of_bound |Real.log b| <| by
    filter_upwards using by simp [Real.logb, mul_div_cancel‚ÇÄ, abs_ne_zero, (log_pos hb).ne']

theorem Nat.log_isBigO_log (b : ‚Ñï) :
    (fun x : ‚Ñï ‚Ü¶ (Nat.log b x : ‚Ñù)) =O[atTop] (fun x : ‚Ñï ‚Ü¶ Real.log x) :=
  (Real.natLog_isBigO_logb _).trans
    ((Real.logb_isBigO_log _).comp_tendsto tendsto_natCast_atTop_atTop)

theorem countTriples_isBigO_dyadicSup (Œµ : ‚Ñù) :
    (fun X ‚Ü¶ (countTriples Œµ X : ‚Ñù)) =O[atTop] (fun X ‚Ü¶ (Real.log X) ^ 4 * dyadicSupBound Œµ X) := by
  trans fun X ‚Ü¶ (Nat.log 2 X+1:‚Ñù)^4 * dyadicSupBound Œµ X
  ¬∑ apply IsBigO.of_norm_le
    simp only [Real.norm_natCast, Prod.forall]
    exact_mod_cast fun b ‚Ü¶ countTriples_le_log_pow_mul_sup _ _
  ¬∑ apply IsBigO.mul _ (isBigO_refl ..)
    apply IsBigO.pow
    apply IsBigO.add
    ¬∑ exact Nat.log_isBigO_log 2
    apply IsLittleO.isBigO
    apply Real.isLittleO_const_log_atTop.natCast_atTop

/-- The finite set of `d`-tuples `a i` such that `a i ~ X i` for all `i`. -/
def dyadicTuples {d : ‚Ñï} (X : Fin d ‚Üí ‚Ñï) : Finset (Fin d ‚Üí ‚Ñï) :=
  Fintype.piFinset (fun i ‚Ü¶ Finset.Icc (X i) (2 * X i))

@[simp]
theorem mem_dyadicTuples {d : ‚Ñï} (X x : Fin d ‚Üí ‚Ñï) :
    x ‚àà dyadicTuples X ‚Üî ‚àÄ i, x i ~ X i := by
  simp [dyadicTuples, similar]
  norm_cast

open Classical in
/-- The finite set counted by `B_d(C, X, Y, X)`. We choose to add `C` as an entry in these tuples,
  as this allows us to write down a surjective map from a union of these sets back to triples
  `(a, b, c)` in `dyadicTriples Œ± Œ≤ Œ≥`. -/
noncomputable def B_finset (d : ‚Ñï) (C : Fin 3 ‚Üí ‚Ñï) (X Y Z : Fin d ‚Üí ‚Ñï) :
    Finset ((Fin d ‚Üí ‚Ñï) √ó (Fin d ‚Üí ‚Ñï) √ó (Fin d ‚Üí ‚Ñï) √ó (Fin 3 ‚Üí ‚Ñï)) :=
  ((dyadicTuples X) √óÀ¢ (dyadicTuples Y) √óÀ¢ (dyadicTuples Z) √óÀ¢ {C}).filter fun ‚ü®x, y, z, c‚ü© ‚Ü¶
    c 0 * ‚àè i, x i ^ (i.val + 1) + c 1 * ‚àè i, y i ^ (i.val + 1) = c 2 * ‚àè i, z i ^ (i.val + 1) ‚àß
    Nat.gcd (c 0 * ‚àè i, x i) (c 1 * ‚àè i, y i) = 1 ‚àß -- TODO: write these as Coprime?
    Nat.gcd (c 0 * ‚àè i, x i) (c 2 * ‚àè i, z i) = 1 ‚àß
    Nat.gcd (c 1 * ‚àè i, y i) (c 2 * ‚àè i, z i) = 1

theorem mem_B_finset (d : ‚Ñï) (C : Fin 3 ‚Üí ‚Ñï) (X Y Z : Fin d ‚Üí ‚Ñï)
    (x y z : Fin d ‚Üí ‚Ñï) (c : Fin 3 ‚Üí ‚Ñï) :
    (x, y, z, c) ‚àà B_finset d C X Y Z ‚Üî
      C = c ‚àß
      (‚àÄ i, x i ~ X i) ‚àß (‚àÄ i, y i ~ Y i) ‚àß (‚àÄ i, z i ~ Z i) ‚àß
      c 0 * ‚àè i, (x i)^(i.val + 1) + c 1 * ‚àè i, (y i)^(i.val + 1) = c 2 * ‚àè i, (z i)^(i.val + 1) ‚àß
      Nat.gcd (c 0 * ‚àè i, (x i)) (c 1 * ‚àè i, (y i)) = 1 ‚àß
      Nat.gcd (c 0 * ‚àè i, (x i)) (c 2 * ‚àè i, (z i)) = 1 ‚àß
      Nat.gcd (c 1 * ‚àè i, (y i)) (c 2 * ‚àè i, (z i)) = 1 := by
  simp only [B_finset, Fin.isValue, Finset.mem_singleton, Finset.mem_filter, Finset.mem_product,
    mem_dyadicTuples, Function.Embedding.coeFn_mk, Prod.mk.injEq]
  tauto

/-- Definition 2.4 -/
noncomputable def B (d : ‚Ñï) (c : Fin 3 ‚Üí ‚Ñï) (X Y Z : Fin d ‚Üí ‚Ñï) : ‚Ñï := (B_finset d c X Y Z).card

theorem Nat.factorization_le_right (p n : ‚Ñï) (hp : p.Prime) : n.factorization p ‚â§ n := by
  refine factorization_le_of_le_pow ?_
  induction n with
  | zero => simp
  | succ n ih =>
    have : 1 ‚â§ p ^ n := by
      apply Nat.one_le_pow
      apply hp.pos
    have : 2 ‚â§ p := hp.two_le
    rw [pow_succ]
    calc _ ‚â§ p^n + p^n := by gcongr
      _ = p^n * 2 := by ring
      _ ‚â§ p^n * p := by gcongr

theorem Nat.ceil_lt_floor (a b : ‚Ñù) (ha : 0 ‚â§ a) (hab : a + 2 ‚â§ b) : ‚åàa‚åâ‚Çä < ‚åäb‚åã‚Çä := by
  exact_mod_cast calc
    ‚åàa‚åâ‚Çä < a + 1 := by
      exact ceil_lt_add_one ha
    _ ‚â§ b - 1 := by
      linarith
    _ < ‚åäb‚åã‚Çä := by
      exact sub_one_lt_floor b

namespace NiceFactorization

/-- The data and assumptions of lemma 2.5. We treat `d` as a free variable constrained by `hd` here
  because `d` appears in a type and this gives the user some leeway to rewrite the value of `d`. -/
class ProofData where
  /-- The value of epsilon in Lemma 2.5-/
  Œµ : ‚Ñù
  hŒµ_pos : 0 < Œµ
  hŒµ : Œµ < 1/2
  /-- `d` in Lemma 2.5 -/
  d : ‚Ñï
  hd : d = ‚åä5/2 * Œµ‚Åª¬π^2‚åã‚Çä
  /-- `n` in Lemma 2.5 -/
  n : ‚Ñï
  /-- `X` in Lemma 2.5 -/
  X : ‚Ñï
  h1n : 1 ‚â§ n
  hnX : n ‚â§ X

open ProofData NiceFactorization
variable [data : ProofData]

/-- `y j` is the product of primes dividing `n` with multiplicity `j`. -/
def y (j : ‚Ñï) : ‚Ñï := ‚àè p ‚àà n.primeFactors with n.factorization p = j, p

@[simp]
private theorem y_zero : y 0 = 1 := by
  simp +contextual [y, Nat.factorization_eq_zero_iff]

private theorem hy_pos (j : ‚Ñï) : 0 < y j := by
  apply Finset.prod_pos
  simp only [Finset.mem_filter, Nat.mem_primeFactors, ne_eq, and_imp, y]
  intro p hp _ _ _
  exact hp.pos

private theorem prod_y_pow_eq_n_subset {s : Finset ‚Ñï}
    (hs : ‚àÄ p, p.Prime ‚Üí p ‚à£ n ‚Üí n.factorization p ‚àà s) :
    ‚àè m ‚àà s, y m ^ m = n := by
  have := h1n
  simp_rw [y]
  conv =>
    rhs
    rw [‚Üê Nat.factorization_prod_pow_eq_self (show n ‚â† 0 by omega)]
  simp_rw [‚Üê Finset.prod_pow]
  rw [Nat.prod_factorization_eq_prod_primeFactors]
  convert Finset.prod_fiberwise_of_maps_to  (f := fun p ‚Ü¶ p ^ n.factorization p)
    (g := n.factorization) (s := n.primeFactors) (t := s) ?_ using 1
  ¬∑ apply Finset.prod_congr rfl
    intro k hk
    apply Finset.prod_congr rfl
    simp only [Finset.mem_filter, Nat.mem_primeFactors, ne_eq, and_imp, y]
    rintro _ _ _ _ rfl
    rfl
  ¬∑ simp only [Nat.mem_primeFactors, ne_eq, Finset.mem_Icc, and_imp, y]
    intro p hp hpn hn'
    apply hs p hp hpn

private theorem prod_y_pow_eq_n : ‚àè m ‚àà Finset.Icc 1 d ‚à™ Finset.Ioc d n, y m ^ m = n := by
  apply prod_y_pow_eq_n_subset
  intro p hp hpn
  simp only [Finset.mem_union, Finset.mem_range, Finset.mem_Icc]
  have : n.factorization p ‚â§ n := Nat.factorization_le_right p n hp
  have : n.factorization p ‚â§ d ‚à® d < n.factorization p := le_or_lt (n.factorization p) d
  simp only [Finset.mem_Ioc]
  have : 1 ‚â§ n.factorization p := by
    rw [‚Üê hp.dvd_iff_one_le_factorization]
    ¬∑ exact hpn
    ¬∑ have := h1n
      omega
  tauto

private theorem p_dvd_y_iff (i : ‚Ñï) (p : ‚Ñï) (hp : p.Prime) : p ‚à£ y i ‚Üí n.factorization p = i := by
  rw [y, Prime.dvd_finset_prod_iff hp.prime]
  simp only [Finset.mem_filter, Nat.mem_primeFactors, ne_eq]
  rintro ‚ü®q, ‚ü®‚ü®hq, _‚ü©, rfl‚ü©, hpq‚ü©
  congr
  rw [eq_comm, ‚Üê hq.dvd_iff_eq hp.ne_one]
  exact hpq

private theorem hy_cop (i j : ‚Ñï) (hij : i ‚â† j) : Nat.Coprime (y i) (y j) := by
  apply Nat.coprime_of_dvd
  intro p hp hpi hpj
  apply p_dvd_y_iff _ _ hp at hpi
  apply p_dvd_y_iff _ _ hp at hpj
  subst hpi hpj
  exact hij rfl

open Function in
omit data in
theorem _root_.Nat.prod_squarefree {Œπ : Type*} (f : Œπ ‚Üí ‚Ñï) {s : Finset Œπ}
    (hf : ‚àÄ i ‚àà s, Squarefree (f i))
    (h : Set.Pairwise s.toSet (Nat.Coprime on f)) :
    Squarefree (‚àè i ‚àà s, f i) := by
  induction s using Finset.cons_induction with
  | empty => simp
  | cons i s his ih =>
    simp only [Finset.prod_cons]
    simp only [mem_cons, forall_eq_or_imp] at hf
    rw [coe_cons, Set.pairwise_insert_of_symmetric_of_notMem _ (by simpa using his)] at h
    swap
    ¬∑ exact Nat.Coprime.symmetric.comap _
    rw [Nat.squarefree_mul]
    ¬∑ exact ‚ü®hf.1, ih hf.2 h.1‚ü©
    apply Nat.Coprime.prod_right
    intro j hj
    exact h.2 j hj

omit data in
theorem _root_.Associated.nat_eq {a b : ‚Ñï} (h : Associated a b) : a = b := by
  rwa [associated_iff_eq] at h

theorem y_squarefree {i : ‚Ñï} : Squarefree (y i) := by
  rw [y]
  apply Nat.prod_squarefree
  ¬∑ simp +contextual [Finset.mem_filter, Nat.mem_primeFactors, Nat.Prime.prime, Prime.squarefree]
  ¬∑ simp +contextual [Nat.coprime_primes, Function.onFun, Set.Pairwise]

private theorem prod_y_eq_radical_n : ‚àè m ‚àà Finset.Icc 1 d ‚à™ Finset.Ioc d n, y m = radical n := by
  conv => rhs; rw [‚Üê prod_y_pow_eq_n]
  rw [radical_prod]
  ¬∑ apply Finset.prod_congr rfl
    simp only [Finset.mem_union, Finset.mem_range, Finset.mem_Icc]
    intro i _
    by_cases hi : i = 0
    ¬∑ simp [hi]
    ¬∑ rw [radical_pow _ (by omega)]
      have := radical_associated (y_squarefree (i := i)).isRadical (hy_pos _).ne'
      exact associated_iff_eq.1 this.symm
  ¬∑ intro i _ j _ hij
    apply Nat.Coprime.isRelPrime
    apply Nat.Coprime.pow_right
    apply Nat.Coprime.pow_left
    apply hy_cop i j hij

/-- `K` in the proof of lemma 2.5 -/
noncomputable def K := ‚åàŒµ‚Åª¬π‚åâ‚Çä

private theorem two_lt_eps_inv : 2 < Œµ‚Åª¬π := by
  have := hŒµ_pos
  rw [‚Üê inv_inv 2]
  gcongr
  linarith only [hŒµ]

private theorem hK_pos : 0 < K := by
  rw [K, Nat.ceil_pos]
  simp [hŒµ_pos]

private theorem two_lt_K : 2 < K := by
  rw [K, Nat.lt_ceil]
  simp [two_lt_eps_inv]

private theorem K_inv_le_eps : (K : ‚Ñù)‚Åª¬π ‚â§ Œµ := by
  rw [inv_le_iff_one_le_mul‚ÇÄ]
  ¬∑ calc
    1 = Œµ * Œµ‚Åª¬π := by
      field_simp [hŒµ_pos.ne.symm]
    _ ‚â§ Œµ * K := by
      have := hŒµ_pos
      gcongr
      rw [K]
      apply Nat.le_ceil
  ¬∑ simp [hK_pos]

private theorem hd_pos : 0 < d := by
  rw [hd, Nat.floor_pos]
  nlinarith only [two_lt_eps_inv]

private instance hd_ne_zero : NeZero d := by
  simp_rw [neZero_iff]
  apply ne_of_gt hd_pos

private theorem hKd : K < d := by
  have := two_lt_eps_inv
  simp_rw [K, hd]
  rw [Nat.lt_iff_add_one_le]
  apply Nat.ceil_lt_floor
  ¬∑ positivity
  nlinarith

private theorem hK_div_d : (K / d : ‚Ñù) ‚â§ Œµ := by
  have := hŒµ
  have := hŒµ_pos
  have := two_lt_eps_inv
  rw [div_le_iff‚ÇÄ (mod_cast hd_pos)]
  simp only [K, d]
  calc
    _ ‚â§ Œµ‚Åª¬π + 1 := by
      apply le_of_lt
      apply Nat.ceil_lt_add_one
      positivity
    _ ‚â§ 5 / 2 * Œµ‚Åª¬π - Œµ := by
      linarith
    _ = Œµ * (5/2 * Œµ‚Åª¬π^2 - 1) := by
      field_simp [hŒµ_pos.ne.symm]
      ring
    _ ‚â§ _ := by
      rw [hd]
      gcongr
      apply le_of_lt
      apply Nat.sub_one_lt_floor

/-- `x` in the proof of lemma 2.5 -/
noncomputable def x (j : Fin d) : ‚Ñï :=
  y (j.val+1) * if j.val + 1 = K then (‚àè m ‚àà Finset.Ioc d n, y m ^ (m / K)) else 1

theorem x_pos (j : Fin d) : 0 < x j := by
  rw [x]
  split_ifs with h
  ¬∑ apply mul_pos
    ¬∑ apply hy_pos
    ¬∑ apply Finset.prod_pos
      intros
      apply pow_pos (hy_pos _)
  ¬∑ simp [hy_pos]

private theorem x_pairwise_coprime (i j : Fin d) (hij : i ‚â† j) : Nat.gcd (x i) (x j) = 1 := by
  have hij' : i.val ‚â† j.val := by
    simp [Fin.val_inj, hij]
  have hij'' : i.val + 1 ‚â† j.val + 1 := by
    simp [Fin.val_inj, hij]
  simp_rw [x]
  rw [‚Üê Nat.coprime_iff_gcd_eq_one]
  split_ifs with hik hjk
  ¬∑ rw [‚Üê hik] at hjk
    exact (hij''.symm hjk).elim
  ¬∑ rw [Nat.coprime_mul_iff_left, mul_one]
    refine ‚ü®hy_cop _ _ hij'', ?_‚ü©
    rw [Nat.coprime_prod_left_iff]
    simp only [Finset.mem_Ioc, and_imp]
    intro m hMm hmn
    apply Nat.Coprime.pow_left
    apply hy_cop
    omega
  ¬∑ /- deduce from above instead? -/
    rw [Nat.coprime_mul_iff_right, mul_one]
    refine ‚ü®hy_cop _ _ hij'', ?_‚ü©
    rw [Nat.coprime_prod_right_iff]
    simp only [Finset.mem_Ioc, and_imp]
    intro m hMm hmn
    apply Nat.Coprime.pow_right
    apply hy_cop
    omega
  ¬∑ simp_rw [mul_one]
    apply hy_cop _ _ hij''


/-- `c` in the proof of lemma 2.5-/
noncomputable def c : ‚Ñï := ‚àè m ‚àà Finset.Ioc d n, y m ^ (m % K)

private theorem c_pos : 0 < c := by
  rw [c]
  apply Finset.prod_pos
  simp only [Finset.mem_Ioc, and_imp]
  intro i _ _
  apply pow_pos
  exact hy_pos _

omit data in
theorem nat_eq_fin_iff {n a : ‚Ñï} {b : Fin n} [NeZero n] (ha : a < n) :
    (a : Fin n) = b ‚Üî a = b.val := by
  rw [‚Üê Fin.val_inj]
  simp only [Fin.val_natCast, Nat.mod_eq_of_lt ha]

omit data in
theorem fin_eq_nat_iff {n a : ‚Ñï} {b : Fin n} [NeZero n] (ha : a < n) :
    b = (a : Fin n) ‚Üî b.val = a := by
  rw [‚Üê Fin.val_inj]
  simp only [Fin.val_natCast, Nat.mod_eq_of_lt ha]

private theorem aux (f : ‚Ñï ‚Üí ‚Ñï) :
    (‚àè i : Fin d, if i.val + 1 = K then f (i.val + 1) else 1) = f K := by
  obtain ‚ü®K', hK'‚ü© := Nat.exists_eq_add_one.mpr hK_pos
  simp +contextual [hK', ‚Üê Finset.prod_filter]
  have : Finset.univ.filter (fun a : Fin d ‚Ü¶ a.val = K') = ({(‚ÜëK' : Fin d)} : Finset (Fin d)) := by
    ext x; simp only [mem_filter, mem_univ, true_and, mem_singleton]
    rw [eq_comm]
    conv => rhs; rw [eq_comm]
    apply (nat_eq_fin_iff _).symm
    have := hKd
    omega
  simp [this]

private theorem c_mul_prod_x_eq_n : c * ‚àè j, x j ^ (j.val + 1) = n := by
  simp_rw [c, x]
  simp_rw [mul_pow, Finset.prod_mul_distrib]
  simp only [ite_pow, one_pow]
  simp +contextual only
  rw [Fin.prod_univ_eq_prod_range (fun i ‚Ü¶ y (i+1) ^ (i+1))]
  rw [mul_comm, mul_assoc, ‚Üê Finset.prod_pow, aux (fun _ ‚Ü¶ _), ‚Üê Finset.prod_mul_distrib]
  simp_rw [‚Üê pow_mul, ‚Üê pow_add, mul_comm _ (K), Nat.div_add_mod]
  conv => rhs; rw [‚Üê prod_y_pow_eq_n]
  rw [Finset.prod_union]
  ¬∑ have : (Finset.range d).map (addRightEmbedding 1) = Finset.Icc 1 d := by
      rw [range_eq_Ico, Finset.map_add_right_Ico, zero_add, Ico_add_one_right_eq_Icc]
    rw [‚Üê this]
    simp
  refine Finset.disjoint_left.mpr ?_
  simp +contextual

private theorem prod_y_large_le_X_pow : ‚àè m ‚àà Finset.Ioc d n, y m ‚â§ (X:‚Ñù) ^ (d‚Åª¬π : ‚Ñù) := by
  have := hnX
  calc
    _ ‚â§ (‚àè m ‚àà Finset.Ioc d n, y m ^ m : ‚Ñù) ^ (d‚Åª¬π : ‚Ñù) := by
      rw [‚Üê Real.finset_prod_rpow]
      ¬∑ push_cast
        apply Finset.prod_le_prod
        ¬∑ intros; positivity
        simp only [Finset.mem_Ioc, and_imp]
        intro i hMi hin
        conv => lhs; rw [‚Üê Real.rpow_one (y i : ‚Ñù)]
        rw [‚Üê Real.rpow_natCast_mul (by simp)]
        gcongr
        ¬∑ norm_cast
          apply hy_pos
        ¬∑ rw [le_mul_inv_iff‚ÇÄ]
          ¬∑ simp [hMi.le]
          ¬∑ simp [hd_pos]
      intros; positivity
    _ ‚â§ (n:‚Ñù) ^ (d‚Åª¬π : ‚Ñù) := by
      gcongr
      norm_cast
      conv => rhs; rw [‚Üê prod_y_pow_eq_n]
      apply Finset.prod_le_prod_of_subset_of_one_le'
      ¬∑ simp
      simp only [Finset.mem_union, Finset.mem_range, Finset.mem_Icc, not_and, not_le, y]
      intro i hi _
      erw [Nat.succ_le]
      simp only [Nat.zero_eq, d, K, x, c]
      by_cases hi0 : i = 0
      ¬∑ simp [hi0]
      apply pow_pos (hy_pos ..)
    _ ‚â§ _ := by
      gcongr

private theorem c_le_X_pow : c ‚â§ (X:‚Ñù) ^ Œµ := calc
  c ‚â§ ‚àè m ‚àà Finset.Ioc d n, (y m : ‚Ñù) ^ K := by
    simp_rw [c]
    push_cast
    apply Finset.prod_le_prod
    ¬∑ intros; positivity
    simp only [Finset.mem_Ioc, and_imp]
    intro x hMx hxn
    gcongr
    ¬∑ simp only [Nat.one_le_cast, *]
      apply hy_pos
    ¬∑ apply (Nat.mod_lt ..).le
      exact hK_pos
  _ ‚â§ (X ^ (d‚Åª¬π : ‚Ñù)) ^ (K:‚Ñù) := by
    simp only [Real.rpow_natCast]
    rw [Finset.prod_pow]
    gcongr
    exact_mod_cast prod_y_large_le_X_pow
  _ = (X:‚Ñù) ^ (K/d : ‚Ñù) := by
    rw [‚Üê Real.rpow_mul]
    ¬∑ ring_nf
    positivity
  _ ‚â§ X ^ Œµ := by
    have := h1n
    have := hnX
    have := hK_div_d
    gcongr
    ¬∑ norm_cast
      linarith

@[simp]
private theorem tmp'' : ((K-1: Fin d).val + 1) = K := by
  have : (K:Fin d) - 1 = ((K-1: ‚Ñï) : Fin d) := by
    rw [Nat.cast_sub]
    ¬∑ simp
    have := two_lt_K
    omega
  rw [this]
  simp only [Fin.val_natCast]
  rw [Nat.mod_eq_of_lt]
  ¬∑ simp [Nat.add_one_le_iff.eq ‚ñ∏ hK_pos]
  ¬∑ have := hKd
    omega

private theorem radical_le_X_pow_mul_prod : (radical n : ‚Ñï) ‚â§ (X : ‚Ñù)^Œµ * ‚àè j, x j := calc
    (radical n : ‚Ñï) ‚â§ ((radical c : ‚Ñï) : ‚Ñù) * radical (‚àè j, x j ^ (j.val + 1)) := by
      norm_cast
      apply Nat.le_of_dvd
      ¬∑ positivity
      rw [‚Üê c_mul_prod_x_eq_n]
      apply radical_mul_dvd
    _ ‚â§ ((radical c : ‚Ñï) : ‚Ñù)* ‚àè j, (radical (x j ^ (j.val + 1)) : ‚Ñï) := by
      gcongr
      apply Nat.le_of_dvd
      ¬∑ positivity
      apply radical_prod_dvd
    _ = ((radical c : ‚Ñï) : ‚Ñù)* ‚àè j, (radical (x j) : ‚Ñï) := by
      congr 3 with j
      by_cases hj : (j : ‚Ñï) = 0
      ¬∑ simp [hj]
      rw [radical_pow]
      omega
    _ ‚â§ (X : ‚Ñù)^Œµ * ‚àè j, x j := by
      gcongr
      ¬∑ calc
          _ ‚â§ ‚Üëc := by
            norm_cast
            apply Nat.le_of_dvd c_pos
            apply radical_dvd_self
          _ ‚â§ (_:‚Ñù) := by
            apply c_le_X_pow
      apply Nat.le_of_dvd
      ¬∑ apply x_pos
      apply radical_dvd_self

theorem x_K_le_X_pow : x (K-1) ‚â§ (X : ‚Ñù) ^ Œµ := by
  have h1n := h1n
  have hnX := hnX
  rw [x, if_pos ?side]
  case side =>
    simp
  have := hKd
  simp only [tmp'', Nat.cast_mul, Nat.cast_prod, Nat.cast_pow, ge_iff_le]
  exact_mod_cast calc
    (y K * ‚àè m ‚àà Finset.Ioc d n, y m ^ (m / K) : ‚Ñù) ‚â§
      (y K ^ K)^(K‚Åª¬π:‚Ñù) * (‚àè m ‚àà Finset.Ioc d n, y m ^ m) ^ (K‚Åª¬π:‚Ñù)  := by
        gcongr
        ¬∑ rw [‚Üê Real.rpow_natCast_mul, mul_inv_cancel‚ÇÄ]
          ¬∑ simp
          ¬∑ simp only [ne_eq, Nat.cast_eq_zero]
            apply hK_pos.ne.symm
          ¬∑ exact_mod_cast (hy_pos _).le
        ¬∑ push_cast
          rw [‚Üê Real.finset_prod_rpow _ _ (by simp)]
          gcongr with i hi
          rw [‚Üê Real.rpow_natCast_mul, ‚Üê Real.rpow_natCast]
          ¬∑ gcongr
            ¬∑ simp only [Nat.one_le_cast]
              apply Nat.add_one_le_of_lt
              apply hy_pos
            ¬∑ rw [‚Üê Nat.floor_div_eq_div (K := ‚Ñù), div_eq_mul_inv]
              apply Nat.floor_le
              positivity
          ¬∑ simp
    _ = (‚àè m ‚àà {K} ‚à™ Finset.Ioc d n, y m ^ m) ^ (K‚Åª¬π:‚Ñù)  := by
        rw [Finset.prod_union]
        ¬∑ simp only [Nat.cast_prod, Nat.cast_pow, Finset.prod_singleton, Nat.cast_mul]
          rw [Real.mul_rpow]
          ¬∑ simp
          ¬∑ positivity
        ¬∑ simp only [disjoint_singleton_left, mem_Ioc, not_and, not_le]
          intro
          linarith
    _ ‚â§ (‚àè m ‚àà Finset.Icc 1 d ‚à™ Finset.Ioc d n, y m ^ m) ^ (K‚Åª¬π:‚Ñù)  := by
      gcongr
      ¬∑ intros
        apply Nat.add_one_le_of_lt
        simp [hy_pos]
      ¬∑ intro i
        simp +contextual [hKd.le, Nat.add_one_le_iff.eq ‚ñ∏ hK_pos]
    _ = (n : ‚Ñù) ^ (K‚Åª¬π : ‚Ñù) := by
      congr
      rw [prod_y_pow_eq_n]
    _ ‚â§ (X : ‚Ñù) ^ (K‚Åª¬π : ‚Ñù) := by
      gcongr
    _ ‚â§ (X:‚Ñù)^Œµ := by
      gcongr
      ¬∑ norm_cast
        omega
      apply K_inv_le_eps

private theorem X_pow_mul_prod_le_radical : (X : ‚Ñù)^(-Œµ) * ‚àè j, x j ‚â§ (radical n : ‚Ñï) := calc
    (X : ‚Ñù) ^ (-Œµ) * ‚àè j, x j ‚â§ ‚àè (j : Fin d), if j ‚â† (K-1:‚Ñï) then x j else 1 := by
      rw [Real.rpow_neg (by positivity)]
      apply inv_mul_le_of_le_mul‚ÇÄ
      ¬∑ positivity
      ¬∑ positivity
      ¬∑ calc
          _ ‚â§ (x (K-1):‚Ñù) * ‚àè j : Fin d, if j ‚â† (K-1:‚Ñï) then x j else 1 := by
            norm_cast
            push_cast
            have : (K:Fin d) - 1 = ((K-1: ‚Ñï) : Fin d) := by
              rw [Nat.cast_sub]
              ¬∑ simp
              have := two_lt_K
              omega
            rw [‚Üê Finset.prod_filter, Finset.filter_ne', this, Finset.mul_prod_erase]
            exact Finset.mem_univ _
          _ ‚â§ _ := by
            have := x_K_le_X_pow
            gcongr
    _ = ‚àè j : Fin d, if j.val ‚â† (K-1:‚Ñï) then y (j.val + 1) else 1 := by
      have (j : Fin d) : j.val = (K-1) ‚Üî j = (K-1 : ‚Ñï) := by
        apply (fin_eq_nat_iff _).symm
        have := hKd
        omega
      norm_cast
      simp_rw [‚Üê Finset.prod_filter, this]
      apply Finset.prod_congr
      ¬∑ ext j; simp
      simp only [Finset.mem_filter, Finset.mem_univ, true_and]
      intro j h
      have : j.val + 1 ‚â† K := by
        intro hK
        rw [‚Üê hK] at h
        simp only [add_tsub_cancel_right, Fin.cast_val_eq_self, not_true_eq_false] at h
      simp [x, y, mul_ite, mul_one, ite_eq_right_iff, h, this]
    _ ‚â§ ‚àè j : Fin d, if j.val + 1 ‚â† (K:‚Ñï) then y (j.val + 1) else 1 := by
      gcongr with j
      apply le_of_eq
      apply if_congr _ rfl rfl
      apply Iff.not
      rw [eq_comm, Nat.sub_eq_iff_eq_add, eq_comm]
      have := two_lt_K
      omega
    _ = ‚àè m ‚àà Finset.range d with m + 1 ‚â† K, y (m+1) := by
      norm_cast
      rw [Finset.prod_filter,
        Fin.prod_univ_eq_prod_range (fun j ‚Ü¶ if j + 1 ‚â† K then y (j + 1) else 1) d]
    _ ‚â§ ‚àè m ‚àà Finset.range d, y (m+1) := by
      norm_cast
      apply Finset.prod_le_prod_of_subset_of_one_le'
      ¬∑ exact Finset.filter_subset (fun m ‚Ü¶ m + 1 ‚â† K) (Finset.range d)
      ¬∑ intros
        apply hy_pos
    _ = ‚àè m ‚àà Finset.Icc 1 d, y m := by
      norm_cast
      have : (Finset.range d).map (addRightEmbedding 1) = Finset.Icc 1 d := by
        have : 1 ‚â§ d := by
          apply hd_pos
        rw [Nat.range_eq_Icc_zero_sub_one _ (by omega), Finset.map_add_right_Icc]
        simp [this]
      simp [‚Üê this, Finset.prod_map]
    _ ‚â§ ‚àè m ‚àà Finset.Icc 1 d ‚à™ Finset.Ioc d n, y m := by
      norm_cast
      apply Finset.prod_le_prod_of_subset_of_one_le'
      ¬∑ intro x
        simp +contextual
      ¬∑ simp only [Finset.mem_union, Finset.mem_range, Finset.mem_Icc, Finset.mem_filter, not_and,
          Decidable.not_not, hy_pos]
        intro i _ _
        apply hy_pos
    _ = (radical n : ‚Ñï) := mod_cast prod_y_eq_radical_n

theorem exists_nice_factorization :
  ‚àÉ (x : (Fin d) ‚Üí ‚Ñï), ‚àÉ c : ‚Ñï,
    n = c * ‚àè j, x j ^ (j.val + 1:‚Ñï) ‚àß
    c ‚â§ (X:‚Ñù)^(Œµ) ‚àß
    (‚àÄ i j, i ‚â† j ‚Üí Nat.gcd (x i) (x j) = 1) ‚àß
    (X:‚Ñù)^(- Œµ) * ‚àè j, x j ‚â§ (radical n : ‚Ñï) ‚àß (radical n : ‚Ñï) ‚â§ (X:‚Ñù)^(Œµ) * ‚àè j, x j := by
  refine ‚ü®x, c, c_mul_prod_x_eq_n.symm, c_le_X_pow, x_pairwise_coprime, X_pow_mul_prod_le_radical,
    radical_le_X_pow_mul_prod‚ü©

end NiceFactorization


open NiceFactorization in
/-- Proposition 2.5. The bulk of the proof is in the section `NiceFactorization`. -/
theorem exists_nice_factorization
  {Œµ : ‚Ñù}
  (hŒµ_pos : 0 < Œµ)
  (hŒµ : Œµ < 1/2)
  {d : ‚Ñï}
  (hd : d = ‚åä5/2 * Œµ‚Åª¬π^2‚åã‚Çä)
  {n : ‚Ñï}
  {X : ‚Ñï}
  (h1n : 1 ‚â§ n)
  (hnX : n ‚â§ X) :
  ‚àÉ (x : (Fin d) ‚Üí ‚Ñï), ‚àÉ c : ‚Ñï,
    n = c * ‚àè j, x j ^ (j.val + 1 : ‚Ñï) ‚àß
    c ‚â§ (X : ‚Ñù) ^ Œµ ‚àß
    (‚àÄ i j, i ‚â† j ‚Üí Nat.gcd (x i) (x j) = 1) ‚àß
    (X:‚Ñù)^(- Œµ) * ‚àè j, x j ‚â§ (radical n : ‚Ñï) ‚àß (radical n : ‚Ñï) ‚â§ (X:‚Ñù)^(Œµ) * ‚àè j, x j ‚àß
    0 < c ‚àß (‚àÄ i, 0 < x i) ‚àß (‚àÄ i, x i ‚â§ X) := by
  let data : NiceFactorization.ProofData := ‚ü®
    Œµ, hŒµ_pos, hŒµ, d, hd, n, X, h1n, hnX
  ‚ü©
  obtain ‚ü®x, c, hn, hc, hcop, h_le_rad, h_rad_le‚ü© := NiceFactorization.exists_nice_factorization
  have : NeZero ProofData.d := by infer_instance
  simp_rw [data] at *
  have hc_pos : 0 < c := by
    have : 0 < n := by omega
    apply Nat.pos_of_mul_pos_right (hn ‚ñ∏ this)
  have x_le_X (i : Fin d) : x i ‚â§ X := by
    apply le_trans _ hnX
    apply Nat.le_of_dvd
    ¬∑ omega
    rw [hn]
    apply Dvd.dvd.mul_left
    trans x i ^ (i.val + 1)
    ¬∑ apply dvd_pow (dvd_rfl)
      omega
    apply Finset.dvd_prod_of_mem
    exact Finset.mem_univ i
  have hx_pos (i : Fin d) : 0 < x i := by
    apply Nat.pos_of_ne_zero
    intro h
    rw [Finset.prod_eq_zero (Finset.mem_univ i)] at hn
    ¬∑ omega
    ¬∑ simp [h]
  exact ‚ü®x, c, hn, hc, hcop, h_le_rad, h_rad_le, hc_pos, hx_pos, x_le_X‚ü©

/-- Some basic consequences of Proposition 2.5, phrased in a way that make them more useful in the
  proof of Proposition 2.6. -/
theorem exists_nice_factorization'
  {Œµ : ‚Ñù}
  (hŒµ_pos : 0 < Œµ)
  (hŒµ : Œµ < 1/2)
  {d : ‚Ñï}
  (hd : d = ‚åä10 * Œµ‚Åª¬π^4‚åã‚Çä)
  {n : ‚Ñï}
  {X : ‚Ñï}
  (h1n : 1 ‚â§ n)
  (hnX : n ‚â§ X)
  (Œ± : ‚Ñù)
  (hsim : (radical n : ‚Ñï) ~ (X : ‚Ñù) ^ Œ±) :
  ‚àÉ (x : (Fin d) ‚Üí ‚Ñï), ‚àÉ c : ‚Ñï,
    n = c * ‚àè j, x j ^ (j.val + 1 : ‚Ñï) ‚àß
    c ‚â§ (X : ‚Ñù) ^ (Œµ^2) ‚àß
    c ‚â§ ‚åä(X : ‚Ñù) ^ (Œµ/4)‚åã‚Çä ‚àß
    (‚àÄ i j, i ‚â† j ‚Üí Nat.gcd (x i) (x j) = 1) ‚àß
    (X : ‚Ñù)^(Œ± - Œµ) ‚â§ ‚àè j, x j ‚àß ‚àè j, x j ‚â§ 2 * (X : ‚Ñù)^(Œ± + Œµ) ‚àß
    0 < c ‚àß (‚àÄ i, 0 < x i) ‚àß (‚àÄ i, x i ‚â§ X) := by
  have hŒµ_sq : Œµ^2/2 < 1/2 := by
    nlinarith only [hŒµ, hŒµ_pos]
  have : 0 < X := by omega
  simp only [similar, Set.mem_Icc] at hsim
  obtain ‚ü®x, c, x_eq_c_mul_prod, c_le_pow, hx_cop, le_rad_n, rad_n_le, c_pos, hx_pos, hx_le_X‚ü© :=
    exists_nice_factorization (Œµ := Œµ^2/2) (by positivity) hŒµ_sq (d := d) (by rw [hd]; ring_nf)
      h1n hnX
  refine ‚ü®x, c, x_eq_c_mul_prod, ?_, ?_, hx_cop, ?_, ?_, c_pos, hx_pos, hx_le_X‚ü©
  ¬∑ trans (X : ‚Ñù)^(Œµ^2/2)
    ¬∑ exact c_le_pow
    gcongr
    ¬∑ norm_cast
    ¬∑ linarith [sq_nonneg Œµ]
  ¬∑ rw [Nat.le_floor_iff]
    ¬∑ apply c_le_pow.trans
      gcongr _ ^ ?_
      ¬∑ norm_cast
      ¬∑ nlinarith
    positivity
  ¬∑ rw [sub_eq_add_neg, Real.rpow_add, Real.rpow_neg, mul_inv_le_iff‚ÇÄ]
    ¬∑ apply hsim.1.trans (rad_n_le.trans _)
      rw [mul_comm]
      gcongr
      ¬∑ norm_cast
      ¬∑ nlinarith only [hŒµ, hŒµ_pos]
    ¬∑ positivity
    ¬∑ positivity
    ¬∑ positivity
  ¬∑ rw [Real.rpow_add, ‚Üê mul_assoc, ‚Üê mul_inv_le_iff‚ÇÄ]
    ¬∑ apply le_trans (le_trans _ le_rad_n) hsim.2
      rw [mul_comm, Real.rpow_neg (by positivity)]
      gcongr
      ¬∑ norm_cast
      ¬∑ nlinarith only [hŒµ, hŒµ_pos]
    ¬∑ positivity
    ¬∑ norm_cast


/-- A surjective map ‚ãÉ_{c, X, Y ,Z} B (c, X, Y, Z) ‚Üí S*_Œ± Œ≤ Œ≥ (X) -/
def B_to_triple {d : ‚Ñï} : (Fin d ‚Üí ‚Ñï) √ó (Fin d ‚Üí ‚Ñï) √ó (Fin d ‚Üí ‚Ñï) √ó (Fin 3 ‚Üí ‚Ñï) ‚Üí ‚Ñï √ó ‚Ñï √ó ‚Ñï :=
  fun ‚ü®X, Y, Z, c‚ü© ‚Ü¶
    ‚ü®c 0 * ‚àè i, X i ^ (i.val + 1), c 1 * ‚àè i, Y i ^ (i.val + 1), c 2 * ‚àè i, Z i ^ (i.val + 1)‚ü©

open Classical in
/-- The finite set over which we will take a supremum in proposition 2.6 -/
noncomputable def indexSet' (Œ± Œ≤ Œ≥ : ‚Ñù) (d : ‚Ñï) (x : ‚Ñï) (Œµ : ‚Ñù) :
    Finset ((Fin d ‚Üí ‚Ñï) √ó (Fin d ‚Üí ‚Ñï) √ó (Fin d ‚Üí ‚Ñï) √ó (Fin 3 ‚Üí ‚Ñï)) :=
  ((Fintype.piFinset (fun _ ‚Ü¶ Finset.Icc 0 (Nat.log 2 x))) √óÀ¢
  (Fintype.piFinset (fun _ ‚Ü¶ Finset.Icc 0 (Nat.log 2 x))) √óÀ¢
  (Fintype.piFinset (fun _ ‚Ü¶ Finset.Icc 0 (Nat.log 2 x)) √óÀ¢
  (Fintype.piFinset (fun _ ‚Ü¶ Finset.Ioc 0 ‚åä(x:‚Ñù)^(Œµ/4)‚åã‚Çä) : Finset (Fin 3 ‚Üí ‚Ñï))
  ) |>.filter fun ‚ü®r, s, t, c‚ü© ‚Ü¶
    (x:‚Ñù) ^ (Œ± - Œµ) ‚â§ 2^d * ‚àè i, 2 ^ r i ‚àß ‚àè i, 2 ^ r i ‚â§ 2 * (x:‚Ñù) ^ (Œ± + Œµ) ‚àß
    (x:‚Ñù) ^ (Œ≤ - Œµ) ‚â§ 2^d * ‚àè i, 2 ^ s i ‚àß ‚àè i, 2 ^ s i ‚â§ 2 * (x:‚Ñù) ^ (Œ≤ + Œµ) ‚àß
    (x:‚Ñù) ^ (Œ≥ - Œµ) ‚â§ 2^d * ‚àè i, 2 ^ t i ‚àß ‚àè i, 2 ^ t i ‚â§ 2 * (x:‚Ñù) ^ (Œ≥ + Œµ) ‚àß
    ‚àè i, (2 ^ r i)^(i.val + 1) ‚â§ x ‚àß
    ‚àè i, (2 ^ s i)^(i.val + 1) ‚â§ x ‚àß
    ‚àè i, (2 ^ t i)^(i.val + 1) ‚â§ x ‚àß
    (x : ‚Ñù)^(1-Œµ^2) ‚â§ 2^(Nat.choose (d+1) 2 + 1) * ‚àè i, (2 ^ t i)^(i.val + 1) ‚àß
    (Nat.Coprime (c 0) (c 1)) ‚àß (Nat.Coprime (c 1) (c 2)) ‚àß (Nat.Coprime (c 0) (c 2)))

theorem card_indexSet'_le (Œ± Œ≤ Œ≥ : ‚Ñù) (d : ‚Ñï) (x : ‚Ñï) (Œµ : ‚Ñù)  :
    (indexSet' Œ± Œ≤ Œ≥ d x Œµ).card ‚â§ (Nat.log 2 x + 1)^(3*d) * (‚åä(x:‚Ñù) ^ (Œµ/4)‚åã‚Çä)^3 := by
  rw [indexSet']
  apply Finset.card_filter_le .. |>.trans
  simp only [card_product, Fintype.card_piFinset, Nat.card_Icc, tsub_zero, prod_const, card_univ,
    Fintype.card_fin, Nat.card_Ioc]
  apply le_of_eq
  ring

/-- The union of `B`-sets used in the proof of proposition 2.6. -/
noncomputable def BUnion (Œ± Œ≤ Œ≥ : ‚Ñù) {d : ‚Ñï} (x : ‚Ñï) (Œµ : ‚Ñù) :
    Finset ((Fin d ‚Üí ‚Ñï) √ó (Fin d ‚Üí ‚Ñï) √ó (Fin d ‚Üí ‚Ñï) √ó (Fin 3 ‚Üí ‚Ñï)) :=
  (indexSet' Œ± Œ≤ Œ≥ d x Œµ).sup fun ‚ü®r, s, t, c‚ü© ‚Ü¶
    B_finset d c (fun i ‚Ü¶ 2^r i) (fun i ‚Ü¶ 2^s i) (fun i ‚Ü¶ 2^t i)

theorem similar_pow_log {x : ‚Ñï} (hx : 0 < x) : x ~ 2 ^ Nat.log 2 x := by
  simp only [similar, Set.mem_Icc]
  norm_cast
  constructor
  ¬∑ refine Nat.pow_log_le_self 2 hx.ne.symm
  ¬∑ rw [mul_comm, ‚Üê Nat.pow_succ]
    apply le_of_lt
    refine Nat.lt_pow_succ_log_self ?_ x
    norm_num

theorem coprime_mul_prod_aux {Œπ : Type*} {s : Finset Œπ} {f g u v : Œπ ‚Üí ‚Ñï} {a b : ‚Ñï}
    (hu : ‚àÄ i, 0 < u i) (hv : ‚àÄ i, 0 < v i)
    (hcop : Nat.Coprime (a * ‚àè i ‚àà s, (f i) ^ (u i)) (b * ‚àè i ‚àà s, g i ^ (v i))) :
    Nat.Coprime (a * ‚àè i ‚àà s, f i) (b * ‚àè i ‚àà s, g i) := by
  simpa only [Nat.coprime_mul_iff_right, Nat.coprime_mul_iff_left, Nat.coprime_prod_left_iff,
    Nat.coprime_prod_right_iff, hu, Nat.coprime_pow_left_iff, hv, Nat.coprime_pow_right_iff] using
    hcop

open Finset in
theorem Nat.sum_Ico_choose (n k : ‚Ñï) : ‚àë m ‚àà Ico k n, m.choose k = n.choose (k + 1) := by
  rcases le_or_lt n k with h | h
  ¬∑ rw [choose_eq_zero_of_lt (by omega), Ico_eq_empty_of_le h, sum_empty]
  ¬∑ induction n, h using le_induction with
    | base => simp
    | succ n _ ih =>
      rw [‚Üê insert_Ico_right_eq_Ico_add_one (by omega), sum_insert (by simp), ih,
        choose_succ_succ' n]

open Finset in
lemma Nat.sum_range_add_choose' (n k : ‚Ñï) :
    ‚àë i ‚àà Finset.range n, (i + k).choose k = (n + k).choose (k + 1) := by
  rw [‚Üê sum_Ico_choose, range_eq_Ico]
  convert (sum_map _ (addRightEmbedding k) (¬∑.choose k)).symm using 2
  rw [Finset.map_add_right_Ico, zero_add]

theorem sum_range_id_add_one {d : ‚Ñï} : ‚àë i ‚àà Finset.range d, (i + 1) = (d + 1).choose 2 := by
  simpa using Nat.sum_range_add_choose' d 1

theorem B_to_triple_surjOn {Œ± Œ≤ Œ≥ : ‚Ñù}  (x : ‚Ñï) (Œµ : ‚Ñù) (hŒµ_pos : 0 < Œµ) (hŒµ : Œµ < 1/2) {d : ‚Ñï}
    (hd : d = ‚åä10 * Œµ‚Åª¬π ^ 4‚åã‚Çä) :
    Set.SurjOn (B_to_triple (d := d)) (BUnion Œ± Œ≤ Œ≥ x Œµ).toSet (dyadicPoints Œ± Œ≤ Œ≥ x).toSet := by
  intro ‚ü®a, b, c‚ü©
  simp only [Finset.mem_coe, mem_dyadicPoints, BUnion, Set.mem_image, Finset.mem_sup,
    Fintype.mem_piFinset, Finset.mem_Icc, zero_le, true_and, Prod.exists, and_imp]
  intro ha hb hc hab hac hbc habc hrada hradb hradc hxc hcx
  obtain ‚ü®u, c‚ÇÄ, a_eq_c_mul_prod, _, c‚ÇÄ_le_floor, hu_cop, x_pow_Œ±_le, le_x_pow_Œ±,
    c‚ÇÄ_pos, hu_pos, _‚ü© :=
    exists_nice_factorization' hŒµ_pos hŒµ hd ha (show a ‚â§ x by linarith) _ hrada
  obtain ‚ü®v, c‚ÇÅ, b_eq_c_mul_prod, _, c‚ÇÅ_le_floor, hv_cop, x_pow_Œ≤_le, le_x_pow_Œ≤,
    c‚ÇÅ_pos, hv_pos, _‚ü© :=
    exists_nice_factorization' hŒµ_pos hŒµ hd hb (show b ‚â§ x by linarith) _ hradb
  obtain ‚ü®w, c‚ÇÇ, c_eq_c_mul_prod, c‚ÇÇ_le_pow, c‚ÇÇ_le_floor, hw_cop, x_pow_Œ≥_le,
    le_x_pow_Œ≥, c‚ÇÇ_pos, hw_pos, _‚ü© :=
    exists_nice_factorization' hŒµ_pos hŒµ hd hc (show c ‚â§ x by linarith) _ hradc
  have hax : a ‚â§ x := by omega
  have hbx : b ‚â§ x := by omega
  have hcx : c ‚â§ x := by omega

  let c' : Fin 3 ‚Üí ‚Ñï := ![c‚ÇÄ, c‚ÇÅ, c‚ÇÇ]

  have prod_pow_le {u : Fin d ‚Üí ‚Ñï} (h : ‚àÄ i, 0 < u i) : ‚àè i, 2 ^ Nat.log 2 (u i) ‚â§ ‚àè i, u i := by
    gcongr with i
    apply Nat.pow_log_le_self
    exact (h i).ne.symm

  have le_prod_pow {u : Fin d ‚Üí ‚Ñï} : ‚àè i, u i ‚â§ 2 ^ d * ‚àè i, 2 ^ Nat.log 2 (u i) := calc
    _ ‚â§ ‚àè i, 2 ^ (Nat.log 2 (u i) + 1) := by
      gcongr with i
      apply (Nat.lt_pow_succ_log_self ..).le
      norm_num
    _ = _ := by
      simp [Nat.pow_add, Finset.prod_mul_distrib]
      ring

  have prod_log_pow_le_prod_pow {u : Fin d ‚Üí ‚Ñï} (hu : ‚àÄ i, 0 < u i):
      ‚àè i, (2 ^ Nat.log 2 (u i)) ^ (i.val + 1) ‚â§ ‚àè i, u i ^ (i.val + 1) := by
    apply Finset.prod_le_prod
    ¬∑ simp
    simp only [Finset.mem_univ, forall_const]
    intro i
    gcongr
    refine Nat.pow_log_le_self 2 ?_
    apply (hu _).ne.symm

  refine ‚ü®u, v, w, c', ?_, ?easy‚ü©
  case easy =>
    simp only [B_to_triple, Fin.isValue, Matrix.cons_val_zero, Matrix.cons_val_one, Matrix.cons_val,
      Prod.mk.injEq, c']
    refine ‚ü®a_eq_c_mul_prod.symm, b_eq_c_mul_prod.symm, c_eq_c_mul_prod.symm‚ü©
  refine ‚ü®fun i ‚Ü¶ Nat.log 2 (u i), fun i ‚Ü¶ Nat.log 2 (v i), fun i ‚Ü¶ Nat.log 2 (w i), c', ?_, ?_‚ü©
  ¬∑ simp only [indexSet', Fin.isValue, mem_filter, mem_product, Fintype.mem_piFinset, mem_Icc,
      zero_le, true_and, mem_Ioc]
    refine ‚ü®?_, ?_‚ü©
    ¬∑ refine ‚ü®?_, ?_, ?_, ?_‚ü© <;> try {
      ¬∑ intro i
        apply Nat.log_mono_right
        simp [*] }
      simp only [c']
      intro i
      fin_cases i <;>
        simp [c‚ÇÄ_pos, c‚ÇÅ_pos, c‚ÇÇ_pos, c‚ÇÄ_le_floor, c‚ÇÅ_le_floor, c‚ÇÇ_le_floor]
    refine ‚ü®x_pow_Œ±_le.trans (mod_cast le_prod_pow),
      le_trans (mod_cast (prod_pow_le hu_pos)) le_x_pow_Œ±,
      x_pow_Œ≤_le.trans (mod_cast le_prod_pow),
      le_trans (mod_cast (prod_pow_le hv_pos)) le_x_pow_Œ≤,
      x_pow_Œ≥_le.trans (mod_cast le_prod_pow),
      le_trans (mod_cast (prod_pow_le hw_pos)) le_x_pow_Œ≥, ?_‚ü©
    refine ‚ü®?_, ?_, ?_, ?_, ?_‚ü©
    ¬∑ apply (prod_log_pow_le_prod_pow hu_pos).trans
        ((a_eq_c_mul_prod ‚ñ∏ Nat.le_mul_of_pos_left _ c‚ÇÄ_pos).trans hax)
    ¬∑ apply (prod_log_pow_le_prod_pow hv_pos).trans
        ((b_eq_c_mul_prod ‚ñ∏ Nat.le_mul_of_pos_left _ c‚ÇÅ_pos).trans hbx)
    ¬∑ apply (prod_log_pow_le_prod_pow hw_pos).trans
        ((c_eq_c_mul_prod ‚ñ∏ Nat.le_mul_of_pos_left _ c‚ÇÇ_pos).trans hcx)
    ¬∑ calc
        _ ‚â§ 2 * (‚àè i, w i^(i.val+1) : ‚Ñù):= by
          rw [Real.rpow_sub, div_eq_mul_inv, mul_inv_le_iff‚ÇÄ, mul_comm]
          ¬∑ simp only [Real.rpow_one]
            trans 2 * (c‚ÇÇ *(‚àè i, (w i : ‚Ñù) ^ (i.val + 1)))
            ¬∑ norm_cast
              rw [‚Üê c_eq_c_mul_prod]
              apply hxc
            ¬∑ rw [‚Üê mul_assoc, mul_comm 2, mul_assoc]
              gcongr
          ¬∑ apply Real.rpow_pos_of_pos
            norm_cast
            omega
          ¬∑ norm_cast
            omega
        _ ‚â§ 2 * (‚àè i, (2 ^ (Nat.log 2 (w i)+1))^(i.val+1) : ‚Ñù):= by
          norm_cast
          gcongr _ * ?_
          apply Finset.prod_le_prod
          ¬∑ simp
          intro i _
          gcongr
          rw [‚Üê Nat.succ_eq_add_one]
          apply le_of_lt
          apply Nat.lt_pow_succ_log_self
          norm_num
        _ = _ := by
          rw [add_comm, pow_add _ 1, pow_one, mul_assoc]
          congr 1
          norm_cast
          conv =>
            lhs
            right
            ext i;
            rw [pow_add 2 _ 1, pow_one, mul_pow, mul_comm]
          simp_rw [Finset.prod_mul_distrib]
          rw [Finset.prod_pow_eq_pow_sum]
          congr
          rw [Finset.sum_fin_eq_sum_range]
          simp +contextual only [‚Üê mem_range, ‚ÜìreduceDIte]
          apply sum_range_id_add_one
    ¬∑ simp only [Fin.isValue, Matrix.cons_val_zero, Matrix.cons_val_one, Matrix.cons_val, c']
      simp_rw [a_eq_c_mul_prod, b_eq_c_mul_prod, c_eq_c_mul_prod] at hab hbc hac
      refine ‚ü®hab.coprime_mul_right.coprime_mul_right_right,
        hbc.coprime_mul_right.coprime_mul_right_right,
        hac.coprime_mul_right.coprime_mul_right_right‚ü©
  ¬∑ simp only [mem_B_finset, Nat.cast_pow, Nat.cast_ofNat, Fin.isValue, true_and, c']
    simp only [Fin.isValue, Matrix.cons_val_zero, Matrix.cons_val_one, Matrix.head_cons,
      Matrix.cons_val_two, Nat.succ_eq_add_one, Nat.reduceAdd, Matrix.tail_cons, c']
    refine ‚ü®?_, ?_, ?_, ?_, ?_, ?_, ?_‚ü©
    ¬∑ apply fun i ‚Ü¶ (similar_pow_log (hu_pos i))
    ¬∑ apply fun i ‚Ü¶ (similar_pow_log (hv_pos i))
    ¬∑ apply fun i ‚Ü¶ (similar_pow_log (hw_pos i))
    ¬∑ rw [‚Üêa_eq_c_mul_prod, ‚Üêb_eq_c_mul_prod, ‚Üêc_eq_c_mul_prod]
      exact habc
    ¬∑ apply coprime_mul_prod_aux _ _ (a_eq_c_mul_prod ‚ñ∏ b_eq_c_mul_prod ‚ñ∏ hab) <;> omega
    ¬∑ apply coprime_mul_prod_aux _ _ (a_eq_c_mul_prod ‚ñ∏ c_eq_c_mul_prod ‚ñ∏ hac) <;> omega
    ¬∑ apply coprime_mul_prod_aux _ _ (b_eq_c_mul_prod ‚ñ∏ c_eq_c_mul_prod ‚ñ∏ hbc) <;> omega

theorem refinedCountTriplesStar_le_card_BUnion (Œ± Œ≤ Œ≥ : ‚Ñù) {d : ‚Ñï} (x : ‚Ñï) (Œµ : ‚Ñù)
    (hŒµ_pos : 0 < Œµ) (hŒµ : Œµ < 1/2) (hd : d = ‚åä10 * Œµ‚Åª¬π ^ 4‚åã‚Çä) :
    refinedCountTriplesStar Œ± Œ≤ Œ≥ x ‚â§ (BUnion Œ± Œ≤ Œ≥ x Œµ (d := d)).card := by
  rw [refinedCountTriplesStar]
  apply Finset.card_le_card_of_surjOn _ (B_to_triple_surjOn ..)
  ¬∑ exact hŒµ_pos
  ¬∑ exact hŒµ
  ¬∑ exact hd

section Asymptotics
/- TODO: The results in this section should probably be cleaned up - in the end we also lose a
  factor of (log x)^4 going from N to S, perhaps this should also be rolled into this constant. -/

theorem log_le_const_mul_pow {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) (d : ‚Ñï) (hd : 0 < d) :
    ‚àÉ c ‚â• 0, ‚àÄ x : ‚Ñï, (Real.log x)^d ‚â§ c * (x : ‚Ñù)^Œµ := by
  have := (isLittleO_log_rpow_rpow_atTop d hŒµ).isBigO.natCast_atTop
  have := this.nat_of_atTop (l := ‚ä§) ?_
  ¬∑ simp only [Real.rpow_natCast, Asymptotics.isBigO_iff', norm_pow, Real.norm_eq_abs] at this
    simp only [gt_iff_lt, eventually_top] at this
    obtain ‚ü®c, hc_pos, hc‚ü© := this
    refine ‚ü®c, hc_pos.le, ?_‚ü©
    intro x
    calc
      _ ‚â§ _ := by
        gcongr
        exact le_abs_self (Real.log ‚Üëx)
      _ ‚â§ _ := hc x
      _ ‚â§ _ := by
        rw [abs_of_nonneg]
        positivity
  ¬∑ simp [Real.rpow_natCast, hŒµ.ne', hd.ne']

theorem tmp {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) (d : ‚Ñï) (hd : 0 < d) :
    ‚àÉ c, ‚àÄ x : ‚Ñï, 2 ‚â§ x ‚Üí (Nat.log 2 x + 1) ^ (3 * d) ‚â§ c * (x : ‚Ñù)^(Œµ/4) := by
  obtain ‚ü®c, hc_nonneg, hc‚ü© := log_le_const_mul_pow (show 0 < Œµ/4 by linarith) (3*d) (by omega)
  use (Real.log 2)‚Åª¬π ^ (3*d) * c * 2^(Œµ/4)
  intro x hx
  specialize hc (2*x)
  have log_add : Nat.log 2 x + 1 = Nat.log 2 (2 * x) := by
    rw [mul_comm, Nat.log_mul_base]
    ¬∑ norm_num
    omega
  have : (Nat.log 2 (2 * x))^(3*d) ‚â§ (Real.log 2)‚Åª¬π ^ (3*d) * Real.log (2 * x) ^ (3*d) := by
    trans (Real.logb 2 (2 * x))^(3*d)
    ¬∑ gcongr
      rw [‚Üê Real.natFloor_logb_natCast]
      push_cast
      apply Nat.floor_le
      apply Real.logb_nonneg <;> norm_cast
      omega
    ¬∑ apply le_of_eq
      rw [‚Üê Real.log_div_log]
      ring
  calc
    _ ‚â§ ((Nat.log 2 (2 * x)) ^ (3 * d) : ‚Ñù) :=  by
      rw_mod_cast [log_add]
    _ ‚â§ ((Real.log 2)‚Åª¬π ^ (3*d) * c * 2^(Œµ/4) * (x : ‚Ñù)^(Œµ/4): ‚Ñù) :=  by
      apply this.trans
      simp_rw [mul_assoc]
      gcongr
      push_cast at hc
      apply hc.trans
      gcongr
      rw [Real.mul_rpow] <;> norm_num

/-- The implicit constant in the conclusion of proposition 2.6 -/
noncomputable def const (Œµ : ‚Ñù) : ‚Ñù :=
  if h : 0 < Œµ then
    if h' : Œµ < 1/2 then
      let d := ‚åä10 * Œµ‚Åª¬π^4‚åã‚Çä
      have : 2 < Œµ‚Åª¬π := by
        rw [‚Üê show (2:‚Ñù)‚Åª¬π‚Åª¬π = 2 by norm_num]
        gcongr
        linarith
      have : 2 ^ 4 < Œµ‚Åª¬π^4 := by gcongr
      have hd : 0 < d := by rw [Nat.floor_pos]; linarith
      Classical.choose (tmp h d hd)
    else 0
  else 0

theorem const_spec {Œµ : ‚Ñù} (hŒµ_pos : 0 < Œµ) (hŒµ : Œµ < 1/2) :
    let d := ‚åä10 * Œµ‚Åª¬π^4‚åã‚Çä
    ‚àÄ x : ‚Ñï, 2 ‚â§ x ‚Üí (Nat.log 2 x + 1) ^ (3 * d) ‚â§ const Œµ * (x : ‚Ñù)^(Œµ/4) := by
  rw [const, dif_pos hŒµ_pos, dif_pos hŒµ]
  extract_lets d _ _ _ hd
  apply Classical.choose_spec (tmp hŒµ_pos d hd)

theorem const_nonneg {Œµ : ‚Ñù} : 0 ‚â§ const Œµ := by
  by_cases hŒµ_pos : 0 < Œµ
  ¬∑ by_cases hŒµ : Œµ < 1/2
    ¬∑ have := const_spec hŒµ_pos hŒµ 2 le_rfl
      simp only [inv_pow, Nat.cast_ofNat] at this
      have := calc
        0 ‚â§ _ := mod_cast zero_le _
        _ ‚â§ _ := this
      -- surely there's a better lemma that doesn't require strict positivity
      apply nonneg_of_mul_nonneg_left this
      apply Real.rpow_pos_of_pos
      norm_num
    ¬∑ rw [const, dif_pos hŒµ_pos, dif_neg hŒµ]
  ¬∑ rw [const, dif_neg hŒµ_pos]

end Asymptotics

theorem card_indexSet'_le_pow (Œµ Œ± Œ≤ Œ≥ : ‚Ñù) (d x : ‚Ñï) (hd : d = ‚åä10* Œµ‚Åª¬π^4‚åã‚Çä) (hx : 2 ‚â§ x)
    (hŒµ_pos : 0 < Œµ) (hŒµ : Œµ < 1/2) :
    (indexSet' Œ± Œ≤ Œ≥ d x Œµ).card ‚â§ const Œµ * (x:‚Ñù)^Œµ := by
  have := const_spec hŒµ_pos hŒµ x hx
  rw [‚Üê hd] at this
  calc
    _ ‚â§ ((Nat.log 2 x + 1) ^ (3 * d) * ‚åä(x : ‚Ñù) ^ (Œµ / 4)‚åã‚Çä ^ 3 : ‚Ñù) :=
      mod_cast card_indexSet'_le Œ± Œ≤ Œ≥ d x Œµ
    _ ‚â§ (const Œµ * (x : ‚Ñù) ^ (Œµ/4) * (x : ‚Ñù) ^ (3/4 * Œµ) : ‚Ñù) :=  by
      gcongr
      ¬∑ have := const_nonneg (Œµ := Œµ)
        positivity
      ¬∑ trans (x ^ (Œµ / 4)) ^ 3
        ¬∑ gcongr
          apply Nat.floor_le
          positivity
        apply le_of_eq
        rw [‚Üê Real.rpow_mul_natCast (by positivity)]
        ring_nf
    _ = const Œµ * (x : ‚Ñù) ^ Œµ  :=  by
      rw [mul_assoc, ‚Üê Real.rpow_add]
      ¬∑ ring_nf
      ¬∑ positivity

/-- The value of `d` chosen in proposition 2.6 -/
noncomputable def d (Œµ : ‚Ñù) : ‚Ñï := ‚åä10 * Œµ‚Åª¬π ^ 4‚åã‚Çä

/- Proposition 2.7. Reformulated slightly in terms of the existence of a `Finset` whose elements
  have certain properties. As it stands the statement in the blueprint implicitly assumes that
  this `Finset` is nonempty. That might be true, but is rather annoying to prove and unnecessary
  if we just need an upper bound on S*. -/
theorem refinedCountTriplesStar_isBigO_B
  {Œ± Œ≤ Œ≥ : ‚Ñù}
  /- I'm surprised these assumptions are not necessary.
    Shoud think about if I've done something wrong - Arend -/
  -- (hŒ±_pos : 0 < Œ±) (hŒ≤_pos : 0 < Œ≤) (hŒ≥_pos : 0 < Œ≥)
  -- (hŒ±1 : Œ± ‚â§ 1) (hŒ≤1 : Œ≤ ‚â§ 1) (hŒ≥1 : Œ≥ ‚â§ 1)
  {x : ‚Ñï} (h2X : 2 ‚â§ x) {Œµ : ‚Ñù} (hŒµ_pos : 0 < Œµ) (hŒµ : Œµ < 1/2) :
  ‚àÉ s : Finset ((Fin (d Œµ) ‚Üí ‚Ñï) √ó (Fin (d Œµ) ‚Üí ‚Ñï) √ó (Fin (d Œµ) ‚Üí ‚Ñï) √ó (Fin 3 ‚Üí ‚Ñï)),
    refinedCountTriplesStar Œ± Œ≤ Œ≥ x ‚â§
      const Œµ * (x : ‚Ñù) ^ Œµ * ‚Üë(s.sup (fun ‚ü®X, Y, Z, c‚ü© ‚Ü¶ B (d Œµ) c X Y Z): ‚Ñï) ‚àß
    ‚àÄ X Y Z : Fin (d Œµ) ‚Üí ‚Ñï,
    ‚àÄ c : Fin 3 ‚Üí ‚Ñï,
    ‚ü®X, Y, Z, c‚ü© ‚àà s ‚Üí
    (x:‚Ñù)^(Œ± - Œµ) ‚â§ 2 ^ d Œµ * ‚àè j, X j ‚àß ‚àè j, X j ‚â§ 2 * (x : ‚Ñù) ^ (Œ± + Œµ) ‚àß
    (x:‚Ñù)^(Œ≤ - Œµ) ‚â§ 2 ^ d Œµ * ‚àè j, Y j ‚àß ‚àè j, Y j ‚â§ 2 * (x : ‚Ñù) ^ (Œ≤ + Œµ) ‚àß
    (x:‚Ñù)^(Œ≥ - Œµ) ‚â§ 2 ^ d Œµ * ‚àè j, Z j ‚àß ‚àè j, Z j ‚â§ 2 * (x : ‚Ñù) ^ (Œ≥ + Œµ) ‚àß
    ‚àè i, X i ^ (i.val + 1) ‚â§ x ‚àß
    ‚àè i, Y i ^ (i.val + 1) ‚â§ x ‚àß
    ‚àè i, Z i ^ (i.val + 1) ‚â§ x ‚àß
    (x : ‚Ñù) ^ (1 - Œµ^2) ‚â§ 2^(Nat.choose (d Œµ + 1) 2 + 1) * ‚àè i, Z i ^ (i.val + 1) ‚àß
    (Nat.Coprime (c 0) (c 1)) ‚àß (Nat.Coprime (c 1) (c 2)) ‚àß (Nat.Coprime (c 0) (c 2)) ‚àß
    (‚àÄ i, 1 ‚â§ c i) ‚àß
    (‚àÄ i, (c i : ‚Ñù) ‚â§ (x : ‚Ñù) ^ Œµ)
    := by
  have h‚ÇÅ := refinedCountTriplesStar_le_card_BUnion Œ± Œ≤ Œ≥ (d := d Œµ) x Œµ hŒµ_pos hŒµ rfl
  simp_rw [BUnion, Finset.sup_eq_biUnion] at h‚ÇÅ
  have h‚ÇÇ := h‚ÇÅ.trans Finset.card_biUnion_le |>.trans (sum_le_card_mul_sup ..)
  use (indexSet' Œ± Œ≤ Œ≥ (d Œµ) x Œµ).image fun ‚ü®u, v, w, c‚ü© ‚Ü¶
    ‚ü®fun i ‚Ü¶ 2 ^ u i, fun i ‚Ü¶ 2 ^ v i, fun i ‚Ü¶ 2 ^ w i, c‚ü©
  simp only [Finset.sup_image, Finset.mem_image, Prod.mk.injEq, Prod.exists, Nat.cast_prod,
    Nat.cast_pow, forall_exists_index, and_imp]
  refine ‚ü®?_, ?_‚ü©
  ¬∑ calc
      _ ‚â§ ((_ : ‚Ñï) : ‚Ñù) := Nat.cast_le.2 h‚ÇÇ
      _ ‚â§ _ := by
        push_cast
        gcongr
        ¬∑ have := const_nonneg (Œµ := Œµ)
          positivity
        ¬∑ exact card_indexSet'_le_pow Œµ Œ± Œ≤ Œ≥ (d Œµ) x rfl h2X hŒµ_pos hŒµ
        rfl
  rintro X Y Z _ u v w c huvwc rfl rfl rfl rfl
  simp only [Nat.cast_pow, Nat.cast_ofNat]
  revert huvwc
  simp only [indexSet', Finset.mem_filter, Finset.mem_product, Fintype.mem_piFinset, Finset.mem_Icc,
    zero_le, true_and, Finset.mem_Ioc, and_imp]
  rintro _ _ _ hc _ _ _ _ _ _ _ _ _ _ _ _ _
  refine ‚ü®by assumption, by assumption, by assumption, by assumption, by assumption, by assumption,
    by assumption, by assumption, by assumption, by assumption, by assumption, by assumption,
    by assumption, ?_, ?_‚ü©
  ¬∑ intro i
    apply Nat.succ_le_of_lt
    apply hc i |>.1
  ¬∑ intro i
    calc
      (c i : ‚Ñù) ‚â§ (‚åä(x:‚Ñù) ^ (Œµ / 4)‚åã‚Çä : ‚Ñù) := by
        norm_cast
        apply (hc i).2
      _ ‚â§ (x : ‚Ñù)^(Œµ/4) := by
        apply Nat.floor_le
        positivity
      _ ‚â§  _ := by
        gcongr
        ¬∑ norm_cast; omega
        ¬∑ linarith
